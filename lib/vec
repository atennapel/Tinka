import lib/sum;
import lib/nat;
import lib/eq;
import lib/idata;
import lib/singleton;
import lib/void;
import lib/fin;

let -VecF : * -> (Nat -> *) -> Nat -> * = \A R n. Sum (Eq 0 n) ((-m : Nat) ** A ** R m ** (Eq (S m) n));
let -Vec : Nat -> * -> * = \n A. IData (VecF A) n;
let VNil : {-A : *} -> Vec 0 A = \{A}. ICon {_} {VecF A} (Left Refl);
let VCons : {-A : *} -> {-n : Nat} -> A -> Vec n A -> Vec (S n) A
  = \{A} {n} hd tl. ICon {_} {VecF A} (Right (n, hd, tl, Refl));

let elimVec
  : {-A : *} ->
    (-P : {n : Nat} -> Vec n A -> *) ->
    P VNil ->
    (({-n : Nat} -> (v : Vec n A) -> P v) -> {-m : Nat} -> (hd : A) -> (tl : Vec m A) -> P (VCons hd tl)) ->
    {-n : Nat} ->
    (v : Vec n A) ->
    P v
  = \{A} P nil cons {n} v. elimIData {_} {VecF A} P (\rec {m} y. elimSum (\s. P (ICon {_} {VecF A} {m} s))
      (\q. elimEq (\e. P (ICon {_} {VecF A} (Left e))) nil q)
      (\p.
        let -mm = p.0;
        let hd = p.1;
        let tl = p.2;
        let q = p._2._2._2;
        elimEq (\{j} e. P {j} (ICon {_} {VecF A} (Right (mm, hd, tl, e)))) (cons @rec {mm} hd tl) q)
    y) {n} v;

let foldrVec : {-A -R : *} -> {-n : Nat} -> R -> (A -> R -> R) -> Vec n A -> R
  = \{A} {R} {n} nil cons v. elimVec (\_. R) nil (\rec hd tl. cons hd (rec tl)) v;

let lengthVec : {-A : *} -> {-n : Nat} -> Vec n A -> Nat
  = foldrVec 0 (\_. S);

let lengthVecSingleton : {-A : *} -> {-n : Nat} -> Vec n A -> Singleton n
  = elimVec (\{n} _. Singleton n) (MkSingleton 0) (\rec _ tl. mapSingleton S (rec tl));

let mapVec : {-A -B : *} -> {-n : Nat} -> (A -> B) -> Vec n A -> Vec n B
  = \{A} {B} fn v. elimVec (\{n} _. Vec n B) VNil (\rec hd tl. VCons (fn hd) (rec tl)) v;

let headVec : {-A : *} -> {-n : Nat} -> Vec (S n) A -> A
  = \{A} {n} v. elimVec (\{m} _. Eq m (S n) -> A) (\q. absurd (z_neq_S q)) (\_ hd _ _. hd) v Refl;
let tailVec : {-A : *} -> {-n : Nat} -> Vec (S n) A -> Vec n A
  = \{A} {n} v. elimVec (\{m} _. Eq m (S n) -> Vec (pred m) A) (\q. absurd (z_neq_S q)) (\_ _ tl _. tl) v Refl;

let ixVec : {-A : *} -> {-n : Nat} -> Fin n -> Vec n A -> A
  = \{A} {n}. elimFin (\{n} _. Vec n A -> A) headVec (\rec y v. rec y (tailVec v));

[VecF, Vec, @VNil, @VCons, @elimVec, @foldrVec, @lengthVec, @lengthVecSingleton, @mapVec, @headVec, @tailVec, @ixVec]
:
(-VecF : * -> (Nat -> *) -> Nat -> *)
**
(-Vec : Nat -> * -> *)
**
(VNil : {-A : *} -> Vec 0 A)
**
(VCons : {-A : *} -> {-n : Nat} -> A -> Vec n A -> Vec (S n) A)
**
(elimVec
  : {-A : *} ->
    (-P : {n : Nat} -> Vec n A -> *) ->
    P VNil ->
    (({-n : Nat} -> (v : Vec n A) -> P v) -> {-m : Nat} -> (hd : A) -> (tl : Vec m A) -> P (VCons hd tl)) ->
    {-n : Nat} ->
    (v : Vec n A) ->
    P v)
**
(foldrVec : {-A -R : *} -> {-n : Nat} -> R -> (A -> R -> R) -> Vec n A -> R)
**
(lengthVec : {-A : *} -> {-n : Nat} -> Vec n A -> Nat)
**
(lengthVecSingleton : {-A : *} -> {-n : Nat} -> Vec n A -> Singleton n)
**
(mapVec : {-A -B : *} -> {-n : Nat} -> (A -> B) -> Vec n A -> Vec n B)
**
(headVec : {-A : *} -> {-n : Nat} -> Vec (S n) A -> A)
**
(tailVec : {-A : *} -> {-n : Nat} -> Vec (S n) A -> Vec n A)
**
(ixVec : {-A : *} -> {-n : Nat} -> Fin n -> Vec n A -> A)
**
()
